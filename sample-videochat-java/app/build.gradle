buildscript {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.application'

repositories {
    google()
    mavenCentral()
    maven {
        url "https://github.com/QuickBlox/quickblox-android-sdk-releases/raw/master/"
    }
}

android {
    def versionQACode = 1

    compileSdkVersion 31
    buildToolsVersion "31.0.0"
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.quickblox.sample.videochat.java"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 410010
        versionName '4.1.1'
        multiDexEnabled true
    }

    productFlavors {
        dev {
            dimension "default"
            buildConfigField('boolean', "IS_QA", "false")
            buildConfigField("int", "VERSION_QA_CODE", versionQACode.toString())
        }

        qa {
            dimension "default"
            buildConfigField("boolean", "IS_QA", "true")
            buildConfigField("int", "VERSION_QA_CODE", versionQACode.toString())
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            resValue "string", "versionName", "QuickBlox Video Chat Java\nBuild version " + defaultConfig.getVersionName()
        }

        release {
            signingConfig signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-rules.pro'
            resValue "string", "versionName", "QuickBlox Video Chat Java\nBuild version " + defaultConfig.getVersionName()
        }
    }

    signingConfigs {
        debug {
            storeFile file("../cert/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'com.quickblox:quickblox-android-sdk-videochat-webrtc:3.10.1'
    implementation 'com.quickblox:quickblox-android-sdk-messages:3.10.1'

    implementation 'com.google.firebase:firebase-core:21.1.0'
    implementation 'com.navercorp.pulltorefresh:library:3.2.3@aar'

    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.github.johnkil.android-robototextview:robototextview:4.0.0'
}

apply from: "../artifacts.gradle"
apply plugin: 'com.google.gms.google-services'
