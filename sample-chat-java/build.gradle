ext {
    dimensionDefault = 'default'

    lintAbortOnError = false

    // QuickBlox SDK version
    qbSdkVersion = '3.9.1'

    //Firebase
    firebaseCoreVersion = '16.0.8'

    //Material
    materialVersion = '1.0.0'

    //Android X
    fragmentAndroidXVersion = '1.0.0'
    lifecycleViewmodelAndroidXVersion = '1.0.0'
    coreKtxVersion = '1.0.1'

    //RobotoTextView
    robotoTextViewVersion = '4.0.0'

    //Glide
    glideVersion = '3.6.1'

    //CircleImageView
    circleImageVersion = "2.1.0"

    //StickyListenerHeaders
    stickylistheadersVersion = '0.4.2'

    //Swipy
    swipyVersion = '1.2.3'

    //Crashlytics
    crashlyticsVersion = '2.9.5'

    //Fabric
    fabricToolsVersion = '1.27.0'
}

buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath "io.fabric.tools:gradle:$fabricToolsVersion"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    google()
    jcenter()
    maven {
        url "https://github.com/QuickBlox/quickblox-android-sdk-releases/raw/master/"
    }
    maven { url 'https://maven.fabric.io/public' }
    flatDir { dirs 'libs' }
}

android {
    def versionQACode = 4

    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    flavorDimensions dimensionDefault

    defaultConfig {
        applicationId "com.quickblox.sample.chat.java"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 400000
        versionName '4.0.0'
        multiDexEnabled true
    }

    productFlavors {
        dev {
            dimension dimensionDefault
            buildConfigField('boolean', "IS_QA", "false")
            buildConfigField("int", "VERSION_QA_CODE", versionQACode.toString())
        }

        qa {
            dimension dimensionDefault
            buildConfigField("boolean", "IS_QA", "true")
            buildConfigField("int", "VERSION_QA_CODE", versionQACode.toString())
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            proguardFile 'proguard-rules.pro'
            zipAlignEnabled false
            resValue "string", "versionName", "QuickBlox Chat Java\nBuild version " + defaultConfig.getVersionName()
        }

        release {
            signingConfig signingConfigs.debug
            resValue "string", "versionName", "QuickBlox Chat Java\nBuild version " + defaultConfig.getVersionName()
        }
    }

    signingConfigs {
        debug {
            storeFile file("../cert/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {

    implementation "com.quickblox:quickblox-android-sdk-messages:$qbSdkVersion"
    implementation "com.quickblox:quickblox-android-sdk-chat:$qbSdkVersion"
    implementation "com.quickblox:quickblox-android-sdk-content:$qbSdkVersion"

    implementation "com.google.firebase:firebase-core:$firebaseCoreVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.fragment:fragment:$fragmentAndroidXVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycleViewmodelAndroidXVersion"
    implementation "androidx.core:core-ktx:$coreKtxVersion"

    implementation "com.github.johnkil.android-robototextview:robototextview:$robotoTextViewVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    implementation "de.hdodenhof:circleimageview:$circleImageVersion"

    implementation "com.timehop.stickyheadersrecyclerview:library:$stickylistheadersVersion@aar"
    implementation "com.github.orangegangsters:swipy:$swipyVersion@aar"
    implementation("com.crashlytics.sdk.android:crashlytics:$crashlyticsVersion@aar") {
        transitive = true
    }
}

apply from: "../artifacts.gradle"
apply plugin: 'com.google.gms.google-services'